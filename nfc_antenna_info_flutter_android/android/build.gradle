group = "com.bbkdevelopment"
version = "1.0"

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.1.4")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "checkstyle"

android {
    if (project.android.hasProperty("namespace")) {
        namespace = "com.bbkdevelopment"
    }

    compileSdk = 34

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    defaultConfig {
        minSdk = 21
    }

    dependencies {
        testImplementation("org.mockito:mockito-core:5.1.0")
    }

    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}

checkstyle {
    toolVersion = "10.21.1"
    ignoreFailures = false
}

tasks.register('checkstyleGoogleCheck', Checkstyle) {
    configFile = file("config/checkstyle/google_checks.xml")
    source 'src/main/java'
    include '**/*.java'
    exclude '**/generated/**'
    classpath = files()
}

tasks.register('checkstyleBBKCheck', Checkstyle) {
    configFile = file("config/checkstyle/bbk_checks.xml")
    source 'src/main/java'
    include '**/*.java'
    exclude '**/generated/**'
    classpath = files()
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = false
        html.required = true
        html.stylesheet = resources.text.fromFile("config/xsl/checkstyle-author.xsl")
    }
    maxWarnings = 0
}

check.dependsOn checkstyleGoogleCheck, checkstyleBBKCheck
build.dependsOn checkstyleGoogleCheck, checkstyleBBKCheck
